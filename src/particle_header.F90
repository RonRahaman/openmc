module particle_header

  use constants,       only: NEUTRON, ONE, NONE, ZERO
  use geometry_header, only: BASE_UNIVERSE
  use random_lcg,      only: N_STREAMS

  implicit none

!===============================================================================
! LOCALCOORD describes the location of a particle local to a single
! universe. When the geometry consists of nested universes, a particle will have
! a list of coordinates in each level
!===============================================================================

  type LocalCoord
    ! Indices in various arrays for this level
    integer :: cell      = NONE
    integer :: universe  = NONE
    integer :: lattice   = NONE
    integer :: lattice_x = NONE
    integer :: lattice_y = NONE
    integer :: lattice_z = NONE

    ! Particle position and direction for this level
    real(8) :: xyz(3)
    real(8) :: uvw(3)

    ! Is this level rotated?
    logical :: rotated = .false.

    ! Pointer to next (more local) set of coordinates
    type(LocalCoord), pointer :: next => null()
  end type LocalCoord
     
!===============================================================================
! PARTICLE describes the state of a particle being transported through the
! geometry
!===============================================================================

  type Particle
    ! Basic data
    integer(8) :: id            ! Unique ID
    integer    :: type          ! Particle type (n, p, e, etc)

    ! Particle coordinates
    type(LocalCoord), pointer :: coord0 => null() ! coordinates on universe 0
    type(LocalCoord), pointer :: coord  => null() ! coordinates on lowest universe

    ! Other physical data
    real(8)    :: wgt           ! particle weight
    real(8)    :: E             ! energy
    real(8)    :: mu            ! angle of scatter
    logical    :: alive         ! is particle alive?

    ! Pre-collision physical data
    real(8)    :: last_xyz(3)   ! previous coordinates
    real(8)    :: last_wgt      ! pre-collision particle weight
    real(8)    :: last_E        ! pre-collision energy
    real(8)    :: absorb_wgt    ! weight absorbed for survival biasing

    ! What event last took place
    logical    :: fission       ! did the particle cause implicit fission
    integer    :: event         ! scatter, absorption
    integer    :: event_nuclide ! index in nuclides array
    integer    :: event_MT      ! reaction MT

    ! Post-collision physical data
    integer    :: n_bank        ! number of fission sites banked
    real(8)    :: wgt_bank      ! weight of fission sites banked

    ! Indices for various arrays
    integer    :: surface       ! index for surface particle is on
    integer    :: cell_born     ! index for cell particle was born in
    integer    :: material      ! index for current material
    integer    :: last_material ! index for last material

    ! Statistical data
    integer    :: n_collision   ! # of collisions

    ! Track output
    logical    :: write_track = .false.

    ! Data needed to restart a particle stored in a bank after changing domains
    real(8)    :: stored_xyz(3)
    real(8)    :: stored_uvw(3)
    integer(8) :: prn_seed(N_STREAMS)        ! the  next random number seed 
    integer(8) :: xs_seed(N_STREAMS)         ! the previously seed used for xs gen

    ! Index of eband
    integer :: eband

  contains
    procedure :: initialize => initialize_particle
    procedure :: clear => clear_particle
  end type Particle

!===============================================================================
! PARTICLEPOINTER is a pointer to a particle.  Necessary to create an array of
! pointers.
!===============================================================================

type ParticlePointer
  type(Particle), pointer :: ptr => null()
end type ParticlePointer

!===============================================================================
! SENDPARTICLE was implemented by Nick Horelik for domain-decomposed OpenMC.
! 
! In DD OpenMC, it is a copy of the particle data structure for sending to other
! processes via MPI - it's needed to get proper alignment of fields, and it
! provides a way to cut down on some of the info we need to send
!
! In Eband OpenMC, it similarly provides a flat, lower-memory structure for
! banking particles that leave the energy band. The 'sequence' attribute is not
! strictly needed for on-node energy-banding.  
!===============================================================================

  type SendParticle
  
    sequence

    integer(8) :: id
    integer(8) :: prn_seed(N_STREAMS)
    integer(8) :: xs_seed(N_STREAMS)
    real(8)    :: wgt
    real(8)    :: E
    real(8)    :: mu
    real(8)    :: last_wgt
    real(8)    :: last_E
    real(8)    :: absorb_wgt
    real(8)    :: wgt_bank
    real(8)    :: last_xyz(3)
    real(8)    :: stored_xyz(3)
    real(8)    :: stored_uvw(3)
    integer    :: type
    integer    :: event
    integer    :: event_nuclide
    integer    :: event_MT
    integer    :: n_bank
    integer    :: surface
    integer    :: cell_born
    integer    :: n_collision
    integer    :: material
    integer    :: last_material

    ! New addtions, added since Nick's version
    logical    :: fission
    integer    :: eband

  end type SendParticle

contains

!===============================================================================
! DEALLOCATE_COORD removes all levels of coordinates below a given level. This
! is used in distance_to_boundary when the particle moves from a lower universe
! to a higher universe since the data for the lower one is not needed anymore.
!===============================================================================

  recursive subroutine deallocate_coord(coord)

    type(LocalCoord), pointer :: coord

    if (associated(coord)) then 
      ! recursively deallocate lower coordinates
      if (associated(coord % next)) call deallocate_coord(coord%next)

      ! deallocate original coordinate
      deallocate(coord)
    end if

  end subroutine deallocate_coord

!===============================================================================
! INITIALIZE_PARTICLE sets default attributes for a particle from the source
! bank
!===============================================================================

  subroutine initialize_particle(this)

    class(Particle) :: this

    ! Clear coordinate lists
    call this % clear()

    ! Set particle to neutron that's alive
    this % type  = NEUTRON
    this % alive = .true.

    ! clear attributes
    this % surface       = NONE
    this % cell_born     = NONE
    this % material      = NONE
    this % last_material = NONE
    this % wgt           = ONE
    this % last_wgt      = ONE
    this % absorb_wgt    = ZERO
    this % n_bank        = 0
    this % wgt_bank      = ZERO
    this % n_collision   = 0
    this % fission       = .false.

    ! Set up base level coordinates
    allocate(this % coord0)
    this % coord0 % universe = BASE_UNIVERSE
    this % coord             => this % coord0

  end subroutine initialize_particle

!===============================================================================
! CLEAR_PARTICLE
!===============================================================================

  subroutine clear_particle(this)

    class(Particle) :: this

    ! remove any coordinate levels
    call deallocate_coord(this % coord0)

    ! Make sure coord pointer is nullified
    nullify(this % coord)

  end subroutine clear_particle

!============================================================================
! PARTICLE_TO_BUFFER copies particle information to the SendParticle data type
! for sending via MPI
! (originally implemented in Nick Horelik's domain-decomposed OpenMP)
!===============================================================================

  subroutine particle_to_buffer(part, buf)
  
    type(Particle),     intent(in)  :: part
    type(SendParticle), intent(out) :: buf
    
    buf % id              = part % id
    buf % type            = part % type
    buf % wgt             = part % wgt
    buf % E               = part % E
    buf % mu              = part % mu
    buf % last_xyz        = part % last_xyz
    buf % last_wgt        = part % last_wgt
    buf % last_E          = part % last_E
    buf % absorb_wgt      = part % absorb_wgt
    buf % event           = part % event
    buf % event_nuclide   = part % event_nuclide
    buf % event_MT        = part % event_MT
    buf % n_bank          = part % n_bank
    buf % wgt_bank        = part % wgt_bank
    buf % surface         = part % surface
    buf % cell_born       = part % cell_born
    buf % n_collision     = part % n_collision
    buf % stored_xyz      = part % stored_xyz
    buf % stored_uvw      = part % stored_uvw
    buf % material        = part % material
    buf % last_material   = part % last_material
    
    buf % prn_seed        = part % prn_seed
    buf % xs_seed         = part % xs_seed

    ! New addtions, since Nick's version
    buf % fission         = part % fission
    buf % eband           = part % eband
  
  end subroutine particle_to_buffer

!===============================================================================
! BUFFER_TO_PARTICLE copies particle information out of SendParticle buffer
! received via MPI back into a particle data type
! (originally implemented in Nick Horelik's domain-decomposed OpenMP)
!===============================================================================

  ! subroutine buffer_to_particle(buf, part)
  
  !   type(SendParticle), intent(in)  :: buf
  !   type(Particle),     intent(out) :: part
  
  !   part % id              = buf % id
  !   part % type            = buf % type
  !   part % wgt             = buf % wgt
  !   part % E               = buf % E
  !   part % mu              = buf % mu
  !   part % alive           = .true.
  !   part % last_xyz        = buf % last_xyz
  !   part % last_wgt        = buf % last_wgt
  !   part % last_E          = buf % last_E
  !   part % absorb_wgt      = buf % absorb_wgt
  !   part % event           = buf % event
  !   part % event_nuclide   = buf % event_nuclide
  !   part % event_MT        = buf % event_MT
  !   part % n_bank          = buf % n_bank
  !   part % wgt_bank        = buf % wgt_bank
  !   part % surface         = NONE
  !   part % cell_born       = buf % cell_born
  !   part % material        = NONE
  !   part % last_material   = NONE
  !   part % n_collision     = buf % n_collision
  !   part % stored_xyz      = buf % stored_xyz
  !   part % stored_uvw      = buf % stored_uvw
  !   part % material        = buf % material
  !   part % last_material   = buf % last_material

  !   part % prn_seed        = buf % prn_seed
  !   part % xs_seed         = buf % xs_seed

  !   ! New addtions, since Nick's version
  !   part % fission         = buf % fission
  !   part % eband           = buf % eband

  ! end subroutine buffer_to_particle


end module particle_header
